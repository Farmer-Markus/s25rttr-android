def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY');
def buildAsApplication = !buildAsLibrary
if (buildAsApplication) {
    apply plugin: 'com.android.application'
}
else {
    apply plugin: 'com.android.library'
}

android {
    if (buildAsApplication) {
        namespace "org.s25rttr.sdl"
    }
    
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
    
    compileSdkVersion 34
    defaultConfig {
    	applicationId "org.s25rttr.sdl"
        minSdkVersion 28 // 19
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
             cmake {
                 arguments "-DANDROID_APP_PLATFORM=android-28", "-DANDROID_STL=c++_shared"
                 // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                 abiFilters 'arm64-v8a'
             }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
            .dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }
    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            cmake {
                path 'jni/CMakeLists.txt'
            }
        }
       
    }
    lint {
        abortOnError false
    }

    if (project.hasProperty("RELEASE_STORE_FILE") &&
        project.hasProperty("RELEASE_STORE_PASSWORD") &&
        project.hasProperty("RELEASE_KEY_ALIAS") &&
        project.hasProperty("RELEASE_KEY_PASSWORD")) {
        signingConfigs {

            release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD

            v1SigningEnabled true
            v2SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            if(project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig null
            }
        }
    }
    
    sourceSets {
        main {
            def depInst = "$rootDir/libs"
            def abis = android.defaultConfig.externalNativeBuild.cmake.abiFilters

            jniLibs.srcDirs = [
                "$depInst/SDL",
                "$depInst/SDL_mixer",
                "$depInst/miniupnpc",
                "$depInst/lua",
                "$depInst/gl4es",
                "$depInst/bzip2",
                "$depInst/boost"
            ]
        }
    }
}

tasks.register("buildDependencies") {
    def sdkDir = android.sdkDirectory
    def ndkDir = android.ndkDirectory

    def abis = android.defaultConfig.externalNativeBuild.cmake.abiFilters
    def depSrc = "$rootDir/external"
    def depInst = "$rootDir/libs"
    
    def boostLibs = ["libboost_atomic.so",
	    "libboost_chrono.so",
	    "libboost_container.so",
	    "libboost_date_time.so",
	    "libboost_filesystem.so",
	    "libboost_iostreams.so",
	    "libboost_locale.so",
	    "libboost_nowide.so",
	    "libboost_prg_exec_monitor.so",
	    "libboost_program_options.so",
	    "libboost_random.so",
	    "libboost_regex.so",
	    "libboost_thread.so",
	    "libboost_unit_test_framework.so"
    ]
    
    println "Checking dependencies!"

    onlyIf {
        !abis.every { abi ->
            // Gucken ob alle libs da sind
            file("$depInst/SDL/$abi/libSDL2.so").exists() &&
            file("$depInst/SDL_mixer/$abi/libSDL2_mixer.so").exists() &&
            file("$depInst/miniupnpc/$abi/libminiupnpc.so").exists() &&
            file("$depInst/lua/$abi/liblua.so").exists() &&
            file("$depInst/gl4es/$abi/libGL.so").exists() &&
            file("$depInst/bzip2/$abi/libbz2.so").exists() &&
            boostLibs.every { lib ->
                file("$depInst/boost/$abi/$lib").exists()
            }
        }
    }

    doLast {
        println "Building dependencies!"
        
        def cmakeDir = file("$sdkDir/cmake").listFiles()?.findAll { it.isDirectory() }?.sort()?.last()
        if (cmakeDir == null) {
            throw new GradleException("Cmake not found! Please install android cmake for your sdk!!")
        }
        def cmake = "$cmakeDir/bin/cmake"

        println "Using CMake: $cmake"

        if(!file(ndkDir).exists()) {
            throw new GradleException("NDK Root not found! Please install android ndk and set the ANDROID_SDK_ROOT and ANDROID_NDK_ROOT enviroment variables to the installation.")
        }

        abis.each { abi ->
            def buildDir = ".cxx/depBuild/$abi"
            def installPrefix = "$rootDir/libs/$abi"

            exec {
                commandLine( cmake,
                    "-S", depSrc,
                    "-B", buildDir,
                    "-DCMAKE_TOOLCHAIN_FILE=$ndkDir/build/cmake/android.toolchain.cmake",
                    "-DANDROID_ABI=$abi",
                    "-DANDROID_PLATFORM=android-28",
                    "-DCMAKE_BUILD_TYPE=Release",
                    "-DCMAKE_INSTALL_PREFIX=$installPrefix")
            }

            exec {
                workingDir(buildDir)
                commandLine( cmake, "--build", ".", "--", "-j")
            }
        }
    }
}

// Clean up dependency build files but NOT compiled dependency libs!!
tasks.register("cleanDependencyBuild") {
    doLast {
        def buildDir = file(".cxx/depBuild")

        if(buildDir.exists()) {
            buildDir.deleteDir()
        }
    }
}

// hÃ¤nge es an den Standard-clean Task
tasks.named("clean").configure {
    dependsOn("cleanDependencyBuild")
}

afterEvaluate {
    def taskNames = ["assemble", "assembleDebug", "assembleRelease", "bundleDebug", "bundleRelease", "build", "installDebug", "installRelease"]

    taskNames.each { taskName ->
        if (tasks.findByName(taskName)) {
            tasks.named(taskName).configure { task ->
                task.dependsOn("buildDependencies")
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
}
